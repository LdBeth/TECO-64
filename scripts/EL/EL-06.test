! TECO test: Open log file and verify data w/ 1:@EL !
! Requires: TECO-64, Redirect !
! Expects: PASS !

[[enter]]

@^UA|/tmp/teco.txt|         ! Name of log file we create !
@^UB|EL-06.txt|             ! Name of log file we compare against !

1:@EL/^EQA/                 ! Test: open log file !

"U
    @^A/Can't open log file/
    [[FAIL]]
'
``

! Logging starts here !

@^A/hello, world!/         ! Generate an output message !
``
5 < @I/abcdef/ 10I >        ! Insert some lines of text !
0 J                         ! Go to beginning of buffer !
3 L                         ! Move down 3 lines !
1 K                         ! And kill the next line !

! Logging ends here !
``

@EL//                       ! Test: close log file !
``

HK

:@EZ/diff -B -w -q ^EQA ^EQB/

"U
    @^A/Can't compare log files/
    [[FAIL]]
'

G+ 0J

:@S/differ/

"U
    @^A/Log files differ/
    [[FAIL]]
'

[[exit]]

! Smoke test for TECO text editor !

! Function: Get last string length from insert/search !
!  Command: ^S !
!     TECO: PASS !

[[enter]]

[[JABBERWOCKY]]

0J

3@S/Jabberwock/
^S+10 "N                            ! Test: ^S after S command !
    @^A/^S=/ ^S:=
    [[FAIL]]
'

@I/y baby/
^S+6 "N                             ! Test: ^S after I command !
    @^A/^S=/ ^S:=
    [[FAIL]]
'

590,620 XA

GA
^S+30 "N                            ! Test: ^S after Gq command !
    @^A/^S=/ ^S:=
    [[FAIL]]
'

G_
^S+10 "N                            ! Test: ^S after G_ command !
    @^A/^S=/ ^S:=
    [[FAIL]]
'

:@EW|/tmp/teco.tmp|
"E
    [[FAIL]]
'

G*
^S+13 "N                            ! Test: ^S after G* command !
    @^A/^S=/ ^S:=
    [[FAIL]]
'

[[exit]]

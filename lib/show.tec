! show.tec - Show internal variables for TECO-64 !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

! Here when we know we can use extended features !

E1 U1                                   ! Save extended features flag !

0,32 E1                                 !! Allow end of line comments
0,8 E1                                  !! Allow format strings with '='

!! Set up Q-registers to dump information

@^UJ{
    0EJ  @=/  Process ID: %d/
    0:EJ @=/   Parent ID: %d/
    -1EJ @=[    O/S type: %d]
    -2EJ @=[    H/W type: %d]
    -3EJ @=/   Word size: %d/
    -4EJ @=/  (m,n) size: %d/
    -1^T
}

@^UG{
    @^A/   TECO_INIT: / :@EG/INI/ "L :G* | @^A[(n/a)] ' -1^T
    @^A/TECO_LIBRARY: / :@EG/LIB/ "L :G* | @^A[(n/a)] ' -1^T
    @^A/ TECO_MEMORY: / :@EG/MEM/ "L :G* | @^A[(n/a)] ' -1^T
    @^A/ TECO_VTEDIT: / :@EG/VTE/ "L :G* | @^A[(n/a)] ' -1^T
    -1^T
}

@^U1{Q1:@=/E1 = 0x%x: /
    Q1&32768 "N @^A/newline / '
    Q1&16384 "N @^A/repeat / '
    Q1&2048  "N @^A/c_oper / '
    Q1&1024  "N @^A/percent / '
    Q1&512   "N @^A/insert / '
    Q1&256   "N @^A/dollar / '
    Q1&128   "N @^A/radix / '
    Q1&64    "N @^A/prompt / '
    Q1&32    "N @^A/bang / '
    Q1&16    "N @^A/eimacro / '
    Q1&8     "N @^A/equals / '
    Q1&4     "N @^A/ctrl_a / '
    Q1&2     "N @^A/text / '
    Q1&1     "N @^A/xoper / '
    -1^T
}

@^U2{
    E2:@=/E2 = 0x%x: /
    E2&1024 "N @^A/args / '
    E2&512  "N @^A/page / '
    E2&256  "N @^A/quote / ' 
    E2&128  "N @^A/loop / '
    E2&64   "N @^A/n_arg / '
    E2&32   "N @^A/m_arg / '
    E2&16   "N @^A/comma / '
    E2&8    "N @^A/colon / '
    E2&4    "N @^A/atsign / '
    E2&2    "N @^A/oper / '
    E2&1    "N @^A/zero / '
    -1^T
}

@^U3{
    E3:@=/E3 = 0x%x: /
    E3&256 "N @^A/CR_type / '
    E3&128 "N @^A/keepnul / '
    E3&32  "N @^A/noout / '
    E3&16  "N @^A/noin / '
    E3&8   "N @^A/CR_out / '
    E3&4   "N @^A/CR_in / '
    E3&2   "N @^A/smart / '
    E3&1   "N @^A/nopage / '
    -1^T
}

@^U4{
    E4:@=/E4 = 0x%x: /
    E4&4 "N @^A/status / '
    E4&2 "N @^A/fence / '
    E4&1 "N @^A/invert / '
    -1^T
}

@^UD{
    ED :@=/ED = 0x%x: /
    ED&256 "N @^A/nobuffer / '
    ED&64  "N @^A/movedot / '
    ED&32  "N @^A/escape / '
    ED&16  "N @^A/keepdot / '
    ED&2   "N @^A/yank / '
    ED&1   "N @^A/caret / '
    -1^T
}

@^UH{
    EH :@=/EH = 0x%x: /
    EH&16 "N @^A/who / '
    EH&8  "N @^A/where / '
    EH&4  "N @^A/what / '
    @^A/why=/
    EH&3 - 1 "E
        @^A/terse/
    |
        EH&3 - 3 "E
            @^A/verbose/
        |
            @^A/concise/
        '
    '
    -1^T
}

@^UT{
    ET :@=/ET = 0x%x: /
    ET&32768 "N @^A/ctrl_c / '
    ET&8192  "N @^A/accent / '
    ET&4096  "N @^A/eightbit / '
    ET&512   "N @^A/scope / '
    ET&256   "N @^A/truncate / '
    ET&128   "N @^A/abort / '
    ET&64    "N @^A/detach / '
    ET&32    "N @^A/nowait / '
    ET&8     "N @^A/noecho / '
    ET&4     "N @^A/lower / '
    ET&2     "N @^A/rubout / '
    ET&1     "N @^A/image / '
    -1^T
}

@^UW{
    @^A/    terminal: / 0:W=
    @^A/       width: / 1:W=
    @^A/      height: / 2:W=
    @^A/      SEEALL: / 3:W "E :@^A/disabled/ | :@^A/enabled/ '
    @^A/        mark: / 4:W "E :@^A/disabled/ | 4:W-1 = '
    @^A/        hold: / 5:W=
    @^A/    top left: / 6:W=
    @^A/bottom right: / FZ=
    @^A/   scrolling: /
        7:W
        "N 7:W:= @^A/ lines /
            8:W
            "N @^A/(disabled)/ | @^A/(enabled)/ '
        |
            @^A/disabled/
        '
        -1^T
    @^A/capabilities: 0x/ 9:W===
    @^A/    tab stop: / 10:W=
    @^A/   max. line: / 11:W=
}

!! Q-registers all set up; now dump everything

@^A/TECO version /
     0:EO:= ^^.^T -1:EO:= ^^.^T -2:EO= -1^T

MJ                                      !! Dump environment information

MG                                      !! Dump environment variables

!! Dump general purpose variables

B :@=/B -> %-6d  / ^^^ ^T ^E :@=/E = %-6d  / ^^^ ^T ^S @=/S = %-6d/
. :@=/. -> %-6d  / ^^^ ^T ^Q :@=/Q = %-6d  / ^^^ ^T ^X @=/X = %-6d/
Z :@=/Z -> %-6d  / ^^^ ^T ^R :@=/R = %-6d  / ^^^ ^T ^Z @=/Z = %-6d/

-1^T                                    !! Print blank line

M1 M2 M3 M4 MD                          !! Dump E1-E4 and ED flags

@^A/EE = / EE=                          !! Dump EE variable

MH                                      !! Dump EH flags

@^A/ES = / ES=                          !! Dump ES variable

MT                                      !! Dump ET flags

@^A/EU = / EU=                          !! Dump EU variable

@^A/EV = / EV=                          !! Dump EV variable

-1^T                                    !! Print blank line

@^A/Display parameters:/ -1^T

MW                                      !! Dump display variables

Q1 E1                                   ! Restore original flag !

^[^[

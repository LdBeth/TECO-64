! show.tec - List internal variables for TECO-64 !

! Note that we avoid using any of the extended features that depend !
! on the E1 flag, since we shouldn't presume that they are enabled. !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

@^UJ+
    @^A\  Process ID: \  0EJ=
    @^A\   Parent ID: \ 0:EJ=
    @^A\    O/S type: \ -1EJ=
    @^A\    H/W type: \ -2EJ=
    @^A\   Word size: \ -3EJ=
    @^A\  (m,n) size: \ -4EJ=
    -1^T
+

@^UG+
    @^A\   TECO_INIT: \ :@EG\INI\ "L :G* | @^A"(n/a)" ' -1^T
    @^A\TECO_LIBRARY: \ :@EG\LIB\ "L :G* | @^A"(n/a)" ' -1^T
    @^A\ TECO_MEMORY: \ :@EG\MEM\ "L :G* | @^A"(n/a)" ' -1^T
    @^A\ TECO_VTEDIT: \ :@EG\VTE\ "L :G* | @^A"(n/a)" ' -1^T
    -1^T
+

@^U1+@^A\E1 = \ E1:=== @^A\: \
    E1&32768 "N @^A\newline \ '
    E1&1024  "N @^A\percent \ '
    E1&512   "N @^A\insert \ '
    E1&128   "N @^A\radix \ '
    E1&64    "N @^A\prompt \ '
    E1&32    "N @^A\bang \ '
    E1&16    "N @^A\new_ei \ '
    E1&8     "N @^A\equals \ '
    E1&4     "N @^A\ctrl_a \ '
    E1&2     "N @^A\text \ '
    E1&1     "N @^A\xoper \ '
    -1^T
+

@^U2+
    @^A\E2 = \ E2:=== @^A\: \
    E2&2048 "N @^A\escape \ '
    E2&1024 "N @^A\args \ '
    E2&512  "N @^A\page \ '
    E2&256  "N @^A\quote \ ' 
    E2&128  "N @^A\loop \ '
    E2&64   "N @^A\n_arg \ '
    E2&32   "N @^A\m_arg \ '
    E2&16   "N @^A\comma \ '
    E2&8    "N @^A\colon \ '
    E2&4    "N @^A\atsign \ '
    E2&2    "N @^A\oper \ '
    E2&1    "N @^A\zero \ '
    -1^T
+

@^U3+
    @^A\E3 = \ E3:=== @^A\: \
    E3&256 "N @^A\CR_type \ '
    E3&128 "N @^A\keepnul \ '
    E3&32  "N @^A\noout \ '
    E3&16  "N @^A\noin \ '
    E3&8   "N @^A\CR_out \ '
    E3&4   "N @^A\CR_in \ '
    E3&2   "N @^A\smart \ '
    E3&1   "N @^A\nopage \ '
    -1^T
+

@^U4+
    @^A\E4 = \ E4:=== @^A\: \
    E4&4 "N @^A\status \ '
    E4&2 "N @^A\line \ '
    E4&1 "N @^A\invert \ '
    -1^T
+

@^UD+
    @^A\ED = \ ED:=== @^A\: \
    ED&64  "N @^A\movedot \ '
    ED&32  "N @^A\escape \ '
    ED&16  "N @^A\keepdot \ '
    ED&2   "N @^A\yank \ '
    ED&1   "N @^A\caret \ '
    -1^T
+

@^UH+
    @^A\EH = \ EH:=== @^A\: \
    EH&16 "N @^A\func \ '
    EH&8  "N @^A\line \ '
    EH&4  "N @^A\command \ '
             @^A\verbose=\ EH&3=
   -1^T
+

@^UT+
    @^A\ET = \ ET:=== @^A\: \
    ET&32768 "N @^A\ctrl_c \ '
    ET&8192  "N @^A\accent \ '
    ET&4096  "N @^A\eightbit \ '
    ET&512   "N @^A\scope \ '
    ET&256   "N @^A\truncate \ '
    ET&128   "N @^A\abort \ '
    ET&32    "N @^A\nowait \ '
    ET&8     "N @^A\noecho \ '
    ET&4     "N @^A\lower \ '
    ET&2     "N @^A\rubout \ '
    ET&1     "N @^A\image \ '
    -1^T
+

@^UW+
    @^A\    Terminal: type=\ 0:W:= @^A\, characteristics=\ 9:W:===
    @^A\, width = \ 1:W:= @^A\, height = \ 2:W=
    @^A\    SEEALL: \ 3:W "E @^A\disabled\ | @^A\enabled\ '
    @^A\, mark = \ 4:W:=
    @^A\, hold = \ 5:W:=
    @^A\, top dot = \ 6:W:=
    @^A\, scrolling: \
        7:W
        "N 7:W:= @^A\ lines \
            8:W
            "N @^A\(disabled)\ | @^A\(enabled)\ '
        |
            @^A\disabled\
        '
        -1^T
+
@^A\TECO version \
     0:EO:= ^^.^T -1:EO:= ^^.^T -2:EO= -1^T

MJ MG

@^A\B = \ B:= @^A\, dot = \ .:= @^A\, Z = \ Z=
@^A\^E = \     ^E:=
@^A\, ^Q^Q = \ ^Q:=
@^A\, ^Q^R = \ ^R:=
@^A\, ^S = \   ^S:=
@^A\, ^X = \   ^X:=
@^A\, ^Z = \   ^Z=

-1^T M1 M2 M3 M4 MD

@^A\EE = \ EE=

MH

@^A\ES = \ ES=

MT

@^A\EU = \ EU=

@^A\EV = \ EV=

-1^T @^A\Display parameters:\ -1^T

MW

^[^[

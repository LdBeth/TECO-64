+0 UN                   ! Get flag for enabling display mode (for testing) !

! vtedit.tec - sample window initialization macro for TECO-64 !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

QN "E -1W '                             !! Enable display mode

9,7:W ^[                                !! Set 9 lines for command window

1 UZ                                    !! Enable messages

@f1/ CYAN / BLACK /                     !! Set command window colors
@f2/ BLUE / WHITE /                     !! Set edit window colors
@f3/ GREEN / BLACK /                    !! Set status window colors
@f4/ RED / BLACK /                      !! Set divider line colors

E1 U1                                   !! Save E1
0,32768 E1                              !! See if we can set newline mode
32768&E1 U2                             !! Save bit
Q1 E1                                   !! Restore E1

Q2 "E                                   !! Is this a debug build?
    @fm/ INSERT / :@^A"Newline mode not available" /
|                                       !! Yes
    @fm/ INSERT /                       !! Toggle newline mode
        32768&E1 "N
            32768,0 E1
            QZ "N :@^A"Newline mode off" '
        |
            0,32768 E1
            QZ "N :@^A"Newline mode on" '
        '
    /
'

@fm/ A_DELETE / EK HK EX /              !! Cancel edit and exit

!! F1 sets a mark

@fm/ F1 /
    4:W "E                              !! Already have mark?
        .+1,4:W ^[                      !! No, so set mark
        QZ "N 4:W-1 @="Mark set at %d" '
    |                                   !! Mark already set
        .+1-(4:W) "E                    !! Set at current location?
            0,4:W ^[                    !! If so, just clear it
            QZ "N :@^A"Mark reset" '
        |                               !! Else complain
            QZ "N 4:W-1 @="Mark already at %d" '
        '
    '
    /

!! Shift-F1 clears a mark

@fm/ S_F1 /
    4:W "E                              !! Mark already clear?
        QZ "N :@^A"Mark not set" '      !! Yes, so complain
    |                                   !! Else clear it
        0,4:W ^[
        QZ "N :@^A"Mark reset" '
    '
    /

!! F2 cuts from mark to dot and saves the text in Q-register Z

@fm/ F2 /
    4:W "E                              !! Do we have a mark?
        QZ "N :@^A"Mark not set" '      !! No, so complain
    |                                   !! Else copy text and delete it
        4:W-1,. XZ
        4:W-1,. D
        0,4:W ^[
        QZ "N :@^A"Text cut" '
    '
    /

!! F3 copies from mark to dot and saves the text in Q-register Z

@fm/ F3 /
    4:W "E                              !! Do we have a mark?
        QZ "N :@^A"Mark not set" '      !! No, so complain
    |                                   !! Else copy text
        4:W-1,. XZ
        0,4:W ^[
        QZ "N :@^A"Text copied" '
    '
    /

!! F4 pastes the text in Q-register Z

@fm/ F4 /
    :QZ "E                              !! Any text to paste?
        QZ "N :@^A"No text to paste" '  !! No
    |                                   !! Yes
        GZ
        QZ "N :@^A"Text pasted" '
    '
    /

!! Shift-F4 clears the paste buffer (Q-register Z).

@fm/ S_F4 / 0,0 XZ QZ "N :@^A"Text cleared" ' /

@fm/ F12 / (-1 - (3:W)),3:W /           !! Toggle SEEALL mode

^[^[

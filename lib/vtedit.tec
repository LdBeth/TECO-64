+0 UN                   ! Get flag for enabling display mode (for testing) !

! vtedit.tec - sample window initialization macro for TECO-64 !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

QN "E
    -1W                 !! Enable display mode
'

!! The following calculates the total height of the window, and then allocates
!! 20% of that for the command buffer. Users may adjust this according to their
!! own preferences, or alternately just use an fixed height, regardless of
!! window size.

2:W U.A                 !! Get height of edit buffer
7:W %.A ^[              !! Add height of command buffer
(E4&6)"N %.A ^[ '       !! Add status line, if any
(Q.A * 20 / 100) U.A    !! Use 20% of lines for command buffer
Q.A,7:W ^[              !! Enable scrolling region

@f1/cyan/black/         !! Set command window colors
@f2/blue/white/         !! Set text window colors
@f3/cyan/black/         !! Set status window colors
@f4/cyan/black/         !! Set divider line colors

@fm/S_PGDN/P/           !! Next page
@fm/S_PGUP/-P/          !! Previous page
@fm/F1/FL L/            !! Downcase line, advance to next
@fm/S_F1/FL/            !! Downcase line
@fm/F2/FU L/            !! Upcase line, advance to next
@fm/S_F2/FU/            !! Upcase line
@fm/A_DELETE/EK HK EX/  !! Cancel edit and exit

^[^[

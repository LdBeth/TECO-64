+0 UN                   ! Get flag for enabling display mode (for testing) !

! vtedit.tec - sample window initialization macro for TECO-64 !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

QN "E
    -1W                                 !! Enable display mode
'

9,7:W ^[                                !! Set 9 lines for command window

@f1/ CYAN / BLACK /                     !! Set command window colors
@f2/ BLUE / WHITE /                     !! Set edit window colors
@f3/ GREEN / BLACK /                    !! Set status window colors
@f4/ RED / BLACK /                      !! Set divider line colors

@fm/ A_DELETE / EK HK EX /              !! Cancel edit and exit

@fm/ F1 /
        3:W "E 1,3:W                    !! 1st F1 enables SEEALL
        |
            ET&4096 "N 4096,0ET         !! 2nd F1 disables 8-bit display
            |
                0,4096ET 0,3:W          !! 3rd F1 cycles back to start
            '
        '
        /

!! The following are just to show how to set and reset the mark

@fm/ F2 /
    4:W "E (.+1),4:W-1 @="Mark set at %d"
    |
    4:W-1 @="Mark already set at %d"
    '/
@fm/ S_F2 /
    4:W "E :@^A"Mark not set"
    |
    0,4:W ^[ :@^A"Mark reset"
    '/

^[^[

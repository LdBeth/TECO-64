+0 UN                   ! Get flag for enabling display mode (for testing) !

! vtedit.tec - sample window initialization macro for TECO-64 !

0,128ET                 ! Abort on error !

EO - 200 "L
    @^A/Macro requires TECO version 200+/ 13^T 10^T ^C
'

QN "E
    -1W                                 !! Enable display mode
'

9,7:W ^[                                !! Set 9 lines for command window

@f1/ CYAN / BLACK /                     !! Set command window colors
@f2/ BLUE / WHITE /                     !! Set edit window colors
@f3/ GREEN / BLACK /                    !! Set status window colors
@f4/ RED / BLACK /                      !! Set divider line colors

@fm/ A_DELETE / EK HK EX /              !! Cancel edit and exit

!! F1 sets a mark

@fm/ F1 /
    4:W "E
        (.+1),4:W-1 @="Mark set at %d"
    |
        .+1-(4:W) "E
            0,4:W :@^A"Mark reset"
        |
            4:W-1 @="Mark already set at %d"
        '
    '
    /

!! Shift-F1 clears a mark

@fm/ S_F1 /
    4:W "E
        :@^A"Mark not set"
    |
        0,4:W ^[ :@^A"Mark reset"
    '
    /

!! F2 cuts from mark to dot and saves the text in Q-register Z

@fm/ F2 /
    4:W "E
        :@^A"Mark not set"
    |
        4:W-1,. XZ
        4:W-1,. D
        0,4:W
        :@^A"Text cut to Q-register Z"
    '
    /

!! F3 copies from mark to dot and saves the text in Q-register Z

@fm/ F3 /
    4:W "E
        :@^A"Mark not set"
    |
        4:W-1,. XZ
        0,4:W
        :@^A"Text copied to Q-register Z"
    '
    /

!! F4 pastes the text in Q-register Z

@fm/ F4 /
    :QZ "E :@^A"Nothing to paste from Q-register Z"
    |
        GZ
        :@^A"Text pasted from Q-register Z"
    '
    /

@fm/ F12 / (-1 - (3:W)),3:W /           !! Toggle SEEALL mode

^[^[

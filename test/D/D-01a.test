! TECO test: Delete characters w/ D !
! Requires: TECO !
! Expects: PASS !

[[enter]]

@I/abcdefghijklmnopqrstuvwxyz 0123456789/ 10@I// Z UA 0 UD 0J

0D                                  ! Test: 0D !
::@S/abcde/ [["U]]
QA-Z-QD [["N]]

D                                   ! Test: D !
1%D ^[ R
::@S/egh/ [["U]]
QA-Z-QD [["N]]

1D                                  ! Test: 1D !
1%D ^[ R
::@S/hjk/ [["U]]
QA-Z-QD [["N]]

4D                                  ! Test: nD !
4%D ^[ R
::@S/kpq/ [["U]]
QA-Z-QD [["N]]

-D                                  ! Test: -D !
%D ^[ R
::@S/prs/ [["U]]
QA-Z-QD [["N]]

-1D                                 ! Test: -1D !
%D ^[ R
::@S/rtu/ [["U]]
QA-Z-QD [["N]]

-2D                                 ! Test: -nD !
2%D ^[ R
::@S/rvw/ [["U]]
QA-Z-QD [["N]]

HK @I/abcdefghijklmnopqrstuvwxyz 0123456789/ 10@I// Z UA 0J

28,31D                              ! Test: m,nD !
R ::@S/0456/ [["U]]
QA-Z-3 [["N]]

[[exit]]
